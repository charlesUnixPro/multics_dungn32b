C Object processors for DUNGEON
C
C COPYRIGHT 1980, 1990, INFOCOM COMPUTERS AND COMMUNICATIONS, CAMBRIDGE MA.
C ALL RIGHTS RESERVED, COMMERCIAL USAGE STRICTLY PROHIBITED
C WRITTEN BY R. M. SUPNIK
C
C 29-Sep-94	RMS	Fixed bugs in KILL MASTER, palantir, dial button,
C			well, slide, bat, global brochure, granite wall,
C			bottle, leaves, broken lamp, beam, robot, thief,
C			troll, me, subscripting, object substitution.
C			Added features to heads, coke bottles, balloon,
C			bucket, stove.
C 30-Jan-94	RMS	Fixed bugs from MS-DOS port.
C 25-Jan-94	RMS	Added ground = sand at sandy beach.
C 30-Jun-92	RMS	Changed file names to lower case.
C
C OAPPLI- Object action routines
C
C Declarations
C
	LOGICAL FUNCTION OAPPLI(RI,ARG)
	IMPLICIT INTEGER (A-Z)
	%include dparam
	LOGICAL SOBJS,NOBJS
	LOGICAL QOPEN,QON,LIT,WASLIT
	LOGICAL MOVETO,RMDESC,CLOCKD
	LOGICAL THIEFP,CYCLOP,TROLLP,BALLOP
	LOGICAL QEMPTY,F,OPNCLS
	PARAMETER (MXSMP=99)
C
C Functions and data
C
C	QOPEN(R)=(OFLAG2(R).AND.OPENBT).NE.0
	QOPEN(R)=AND(OFLAG2(R),OPENBT).NE.0
C	QON(R)=(OFLAG1(R).AND.ONBT).NE.0
	QON(R)=AND(OFLAG1(R),ONBT).NE.0
C
	IF(RI.EQ.0) GO TO 10			! zero is false app.
	IF(RI.LE.MXSMP) GO TO 100		! simple object?
	ODO2=0
	ODI2=0
	IF((PRSO.NE.0).AND.(PRSO.LE.OMAX)) ODO2=ODESC2(PRSO)
	IF(PRSI.NE.0) ODI2=ODESC2(PRSI)
	AV=AVEHIC(WINNER)
	FLOBTS=FLAMBT+LITEBT+ONBT
	OAPPLI=.TRUE.
	WASLIT=LIT(HERE)
C
	GO TO (2000,5000,10000,11000,12000,15000,18000,
	& 19000,20000,22000,25000,26000,32000,35000,39000,40000,
	& 45000,47000,48000,49000,50000,51000,52000,54000,55000,
	& 56000,57000,58000,59000,60000,61000,62000),
	&	(RI-MXSMP)
	CALL BUG(6,RI)
C
C Return here to declare false result.
C
10	OAPPLI=.FALSE.
	RETURN
C
C Return here to test for light source change.
C
50	IF(WASLIT.AND..NOT.LIT(HERE)) CALL RSPEAK(406)
	RETURN
C
C Simple objects, processed externally.
C
100	IF(RI.LT.32) OAPPLI=SOBJS(RI,ARG)
	IF(RI.GE.32) OAPPLI=NOBJS(RI,ARG)
	RETURN
C page
C OAPPLI, PAGE 3
C
C O100--	Machine
C
2000	IF(HERE.NE.MMACH) GO TO 10		! not here? f
	OAPPLI=OPNCLS(MACHI,123,124)		! handle opn/cls.
	RETURN
C
C O101--	Water
C
5000	IF(PRSA.NE.GTHROW) GO TO 5025		! go through?
	CALL RSPEAK(331+RND(3))			! joke.
	RETURN
C
5025	IF((PRSO.EQ.WATER).OR.(PRSO.EQ.GWATE)) GO TO 5100
	CALL RSPEAK(561)			! water is ind obj,
	RETURN					! punt.
C
5100	IF(PRSA.NE.TAKEW) GO TO 5400		! take water?
	IF(PRSI.NE.0) GO TO 5200		! from x?
	IF((OADV(BOTTL).EQ.WINNER).AND.(OCAN(PRSO).NE.BOTTL))
	&	GO TO 5500			! take, have bottle -> put.
	IF(OCAN(PRSO).NE.BOTTL) GO TO 5150	! water in bottle?
	IF(OADV(BOTTL).NE.WINNER) GO TO 5125	! already have bottle?
	CALL RSPEAK(103)			! yes, already have water.
	RETURN
C
5125	PRSO=BOTTL				! take bottle.
	GO TO 10				! do normal take.
C
5150	IF(OCAN(PRSO).EQ.0) GO TO 5300		! if not inside, take.
	PRSI=OCAN(PRSO)				! make into take from.
	GO TO 5250				! check for open.
C
5200	IF(OCAN(PRSO).NE.PRSI) GO TO 5250	! is it inside?
	CALL RSPEAK(1038)			! not in that.
	RETURN
C
5250	IF(QOPEN(PRSI)) GO TO 5300		! is it open?
	CALL RSPSUB(525,ODI2)			! no, not open.
	RETURN
C
5300	IF(AV.NE.0) GO TO 5800			! if in vehicle, put there.
	I=615					! assume player.
	IF(WINNER.NE.PLAYER) I=1081
	CALL RSPEAK(I)				! slips thru fingers.
	RETURN
C
5400	IF(PRSA.NE.PUTW) GO TO 5700		! put water in x?
	IF(PRSI.EQ.BOTTL) GO TO 5500		! in bottle?
C	IF(((OFLAG2(PRSI).AND.VEHBT).NE.0).OR.
C	&  ((AV.NE.0).AND.(PRSI.EQ.AV))) GO TO 5450	! in veh?
          IF(((AND(OFLAG2(PRSI),VEHBT)).NE.0).OR.
	&  ((AV.NE.0).AND.(PRSI.EQ.AV))) GO TO 5450	! in veh?
	CALL RSPSUB(297,ODI2)			! wont go elsewhere.
	CALL NEWSTA(PRSO,0,0,0,0)		! vanish water.
	RETURN
C
5450	CALL NEWSTA(WATER,0,0,PRSI,0)		! water into vehicle.
	CALL RSPSUB(296,ODESC2(PRSI))		! describe.
	RETURN
C
5500	IF(QOPEN(BOTTL)) GO TO 5550		! bottle open?
	CALL RSPEAK(612)			! no, lose.
	RETURN
C
5550	IF(QEMPTY(BOTTL)) GO TO 5600		! open, empty?
	CALL RSPEAK(613)			! no, already full.
	RETURN
C
5600	CALL NEWSTA(WATER,614,0,BOTTL,0)	! take water to bottle.
	RETURN
C
5700	IF((PRSA.NE.DROPW).AND.(PRSA.NE.POURW))
	&	GO TO 5900			! drop, pour?
	IF(AV.NE.0) GO TO 5800			! into vehicle?
	CALL NEWSTA(PRSO,133,0,0,0)		! no, vanishes.
	RETURN
C
5800	CALL NEWSTA(WATER,0,0,AV,0)		! water into his vehicle.
	CALL RSPSUB(296,ODESC2(AV))		! describe.
	RETURN
C
5900	IF(PRSA.NE.THROWW) GO TO 10		! last chance, throw?
	CALL NEWSTA(PRSO,132,0,0,0)		! vanishes.
	RETURN
C page
C OAPPLI, PAGE 4
C
C O102--	Leaf pile
C
10000	IF(PRSA.NE.BURNW) GO TO 10500		! burn?
	IF(QOPEN(GRATE).OR.(RVCLR.NE.0)) GO TO 10050
	RVCLR=1					! leaves moved.
	CALL NEWSTA(GRATE,30,HERE,0,0)		! grating appears.
10050	IF(OADV(PRSO).EQ.PLAYER) GO TO 10100	! was he carrying?
	CALL NEWSTA(LEAVE,158,0,0,0)		! no, burn leaves.
	RETURN
C
10100	CALL NEWSTA(LEAVE,0,HERE,0,0)		! drop leaves.
	IF(HERE.EQ.MTREE) CALL NEWSTA(LEAVE,0,FORE3,0,0)
	CALL JIGSUP(159)			! burn him.
	RETURN
C
10500	IF(PRSA.NE.MOVEW) GO TO 10600		! move?
	CALL RSPEAK(2)				! done.
10550	IF(QOPEN(GRATE).OR.(RVCLR.NE.0)) RETURN	! done before?
	RVCLR=1					! leaves moved.
	CALL NEWSTA(GRATE,30,HERE,0,0)		! grating appears.
	RETURN
C
10600	IF(PRSA.NE.TAKEW) GO TO 10700		! take?
	OAPPLI=.FALSE.				! don't handle here.
	GO TO 10550				! make grate visible.
C
10700	IF((PRSA.NE.LOOKUW).OR.QOPEN(GRATE).OR.(RVCLR.NE.0)) GO TO 10
	CALL RSPEAK(344)			! look under?
	RETURN
C
C O103--	Troll, done externally.
C
11000	OAPPLI=TROLLP(ARG)			! troll processor.
	GO TO 50				! go see if now dark.
C
C O104--	Rusty knife.
C
12000	IF(PRSA.NE.TAKEW) GO TO 12100		! take?
	IF(OADV(SWORD).EQ.WINNER) CALL RSPEAK(160) ! pulse sword.
	GO TO 10
C
12100	IF((((PRSA.NE.ATTACW).AND.(PRSA.NE.KILLW)).OR.
	&	(PRSI.NE.RKNIF)).AND.
	&  (((PRSA.NE.SWINGW).AND.(PRSA.NE.THROWW)).OR.
	&	(PRSO.NE.RKNIF))) GO TO 10
	CALL NEWSTA(RKNIF,0,0,0,0)		! kill knife.
	CALL JIGSUP(161)			! kill him.
	RETURN
C page
C OAPPLI, PAGE 5
C
C O105--	Glacier
C
15000	IF(PRSA.NE.THROWW) GO TO 15500		! throw?
	IF(PRSO.NE.TORCH) GO TO 15400		! torch?
	CALL NEWSTA(ICE,169,0,0,0)		! melt ice.
	ODESC1(TORCH)=174			! mung torch.
	ODESC2(TORCH)=173
C	OFLAG1(TORCH)=OFLAG1(TORCH).AND. .NOT.FLOBTS
	OFLAG1(TORCH)=AND (OFLAG1(TORCH), COMPL(FLOBTS))
	CALL NEWSTA(TORCH,0,STREA,0,0)		! move torch.
	GLACRF=.TRUE.				! glacier gone.
	IF(.NOT.LIT(HERE)) CALL RSPEAK(170)	! in dark?
	RETURN
C
15400	CALL RSPEAK(171)			! joke if not torch.
	GO TO 10				! don't handle.
C
15500	IF((PRSA.NE.MELTW).OR.(PRSO.NE.ICE)) GO TO 10
C	IF((OFLAG1(PRSI).AND.FLOBTS).EQ.FLOBTS) GO TO 15600
	IF((AND(OFLAG1(PRSI),FLOBTS)).EQ.FLOBTS) GO TO 15600
	CALL RSPSUB(298,ODI2)			! cant melt with that.
	RETURN
C
15600	GLACMF=.TRUE.				! partial melt.
	IF(PRSI.NE.TORCH) GO TO 15700		! melt with torch?
	ODESC1(TORCH)=174			! mung torch.
	ODESC2(TORCH)=173
C	OFLAG1(TORCH)=OFLAG1(TORCH).AND. .NOT.FLOBTS
	OFLAG1(TORCH)=AND(OFLAG1(TORCH), COMPL(FLOBTS))
15700	CALL JIGSUP(172)			! drown.
	RETURN
C
C O106--	Black book
C
18000	IF(PRSA.NE.OPENW) GO TO 18100		! open?
	CALL RSPEAK(180)			! joke.
	RETURN
C
18100	IF(PRSA.NE.CLOSEW) GO TO 18200		! close?
	CALL RSPEAK(181)
	RETURN
C
18200	IF(PRSA.NE.BURNW) GO TO 10		! burn?
	CALL NEWSTA(BOOK,0,0,0,0)		! vanish book.
	CALL JIGSUP(182)			! fatal joke.
	RETURN
C page
C OAPPLI, PAGE 6
C
C O107--	Candles
C
19000	IF(ORCAND.NE.0) GO TO 19100		! first ref?
	ORCAND=1				! yes, candles are
	CFLAG(CEVCND)=.TRUE.
	CTICK(CEVCND)=50			! burning when seen.
C
19100	IF(PRSI.EQ.CANDL) GO TO 10		! ignore ind refs.
	IF(PRSA.NE.TRNOFW) GO TO 19200		! turn off?
	I=513					! assume off.
	IF(QON(CANDL)) I=514			! if on, different.
	CFLAG(CEVCND)=.FALSE.			! disable countdown.
C	OFLAG1(CANDL)=OFLAG1(CANDL).AND. .NOT.ONBT
	OFLAG1(CANDL)=AND(OFLAG1(CANDL), COMPL(ONBT))
	CALL RSPEAK(I)
	GO TO 50				! go see if now dark.
C
19200	IF((PRSA.NE.BURNW).AND.(PRSA.NE.TRNONW)) GO TO 10
C	IF((OFLAG1(CANDL).AND.LITEBT).NE.0) GO TO 19300
	IF((AND(OFLAG1(CANDL),LITEBT)).NE.0) GO TO 19300
	CALL RSPEAK(515)			! candles too short.
	RETURN
C
19300	IF(PRSI.NE.0) GO TO 19400		! any flame?
	CALL RSPEAK(516)			! no, lose.
	CALL ORPHAN(-1,ACT,PREP1,CANDL,2,' ',0,0) ! orphan "light candle with"
	PRSWON=.FALSE.
	PRSCON=0
	RETURN
C
19400	IF((PRSI.NE.MATCH).OR. .NOT.QON(MATCH)) GO TO 19500
	I=517					! assume off.
	IF(QON(CANDL)) I=518			! if on, joke.
C	OFLAG1(CANDL)=OFLAG1(CANDL).OR.ONBT	! lite candles.
	OFLAG1(CANDL)=OR(OFLAG1(CANDL),ONBT)	! lite candles.
	CFLAG(CEVCND)=.TRUE.			! resume countdown.
	CALL RSPEAK(I)
	RETURN
C
19500	IF((PRSI.NE.TORCH).OR. .NOT.QON(TORCH)) GO TO 19600
	IF(QON(CANDL)) GO TO 19700		! already on?
	CALL NEWSTA(CANDL,521,0,0,0)		! no, vaporize.
	RETURN
C
19600	CALL RSPEAK(519)			! cant light with that.
	RETURN
C
19700	CALL RSPEAK(520)			! already on.
	RETURN
C
C O108--	Matches
C
20000	IF((PRSA.NE.TRNONW).OR.(PRSO.NE.MATCH)) GO TO 20500
	IF(ORMTCH.NE.0) GO TO 20100		! any matches left?
	CALL RSPEAK(183)			! no, lose.
	RETURN
C
20100	ORMTCH=ORMTCH-1				! decrement no matches.
C	OFLAG1(MATCH)=OFLAG1(MATCH).OR.FLOBTS
	OFLAG1(MATCH)=OR(OFLAG1(MATCH),FLOBTS)
	CFLAG(CEVMAT)=.TRUE.
	CTICK(CEVMAT)=2				! countdown.
	CALL RSPEAK(184)
	RETURN
C
C 20500	IF((PRSA.NE.TRNOFW).OR.((OFLAG1(MATCH).AND.ONBT).EQ.0))
C	&	GO TO 10			! extinguish?
20500	IF((PRSA.NE.TRNOFW).OR.((AND(OFLAG1(MATCH),ONBT)).EQ.0))
	&	GO TO 10			! extinguish?
C	OFLAG1(MATCH)=OFLAG1(MATCH).AND. .NOT.FLOBTS
	OFLAG1(MATCH)=AND(OFLAG1(MATCH), COMPL(FLOBTS))
	CTICK(CEVMAT)=0
	CALL RSPEAK(185)
	GO TO 50				! go see if now dark.
C
C O109--	Cyclops, processed externally.
C
22000	OAPPLI=CYCLOP(ARG)			! cyclops
	GO TO 50				! go see if now dark.
C
C O110--	Thief, processed externally.
C
25000	OAPPLI=THIEFP(ARG)
	GO TO 50				! go see if now dark.
C
C O111--	Window
C
26000	OAPPLI=OPNCLS(WINDO,208,209)		! open/cls window.
	RETURN
C
C O112--	Pile of bodies
C
32000	IF(PRSA.NE.TAKEW) GO TO 32500		! take?
	CALL RSPEAK(228)			! cant.
	RETURN
C
32500	IF((PRSA.NE.BURNW).AND.(PRSA.NE.MUNGW)) GO TO 10
	IF(ONPOLF) RETURN			! burn or mung?
	ONPOLF=.TRUE.				! set head on pole.
	CALL NEWSTA(HPOLE,0,LLD2,0,0)
	CALL JIGSUP(229)			! beheaded.
	RETURN
C
C O113--	Vampire bat
C
35000	CALL RSPEAK(50)				! time to fly, jack.
	F=MOVETO(BATDRP(RND(9)+1),WINNER)	! select random dest.
	F=RMDESC(0)
	PRSCON=0				! disable parser.
	RETURN
C page
C OAPPLI, PAGE 7
C
C O114--	Stick
C
39000	IF(PRSA.NE.WAVEW) GO TO 10		! wave?
	IF(HERE.EQ.MRAIN) GO TO 39500		! on rainbow?
	IF((HERE.EQ.POG).OR.(HERE.EQ.FALLS)) GO TO 39200
	CALL RSPEAK(244)			! nothing happens.
	RETURN
C
C 39200	OFLAG1(POT)=OFLAG1(POT).OR.VISIBT	! make gold visible.
39200	OFLAG1(POT)=OR(OFLAG1(POT),VISIBT)	! make gold visible.
	RAINBF=.NOT. RAINBF			! complement rainbow.
	I=245					! assume off.
	IF(RAINBF) I=246			! if on, solid.
	CALL RSPEAK(I)				! describe.
	RETURN
C
39500	RAINBF=.FALSE.				! on rainbow,
	CALL JIGSUP(247)			! take a fall.
	RETURN
C
C O115--	Balloon, handled externally.
C
40000	OAPPLI=BALLOP(ARG)
	RETURN
C
C O116--	Heads
C
45000	IF(PRSA.NE.HELLOW) GO TO 45100		! hello heads?
	CALL RSPEAK(633)			! truly bizarre.
	RETURN
C
45100	IF((PRSA.NE.KILLW).AND.(PRSA.NE.MUNGW).AND.
	&  (PRSA.NE.RUBW).AND.(PRSA.NE.OPENW).AND.
	&  (PRSA.NE.TAKEW).AND.(PRSA.NE.BURNW).AND.(PRSA.NE.SPINW).AND.
	&  (PRSA.NE.ATTACW).AND.(PRSA.NE.KICKW)) GO TO 10
	CALL RSPEAK(260)			! bad news for player
	I=ROBADV(WINNER,0,LCASE,0)+ROBRM(HERE,100,0,LCASE,0)
	IF(I.NE.0) CALL NEWSTA(LCASE,0,LROOM,0,0) ! if robbed, make large case.
	CALL JIGSUP(261)			! kill him.
	RETURN
C page
C OAPPLI, PAGE 8
C
C O117--	Sphere
C
47000	IF(CAGESF.OR.(PRSA.NE.TAKEW)) GO TO 47600 ! take?
	IF(WINNER.NE.PLAYER) GO TO 47500	! robot take?
	CALL RSPEAK(263)			! no, drop cage.
	IF(OROOM(ROBOT).NE.HERE) GO TO 47200	! robot here?
	F=MOVETO(CAGED,WINNER)			! yes, move into cage.
	CALL NEWSTA(ROBOT,0,CAGED,0,0)		! move robot.
	AROOM(AROBOT)=CAGED
C	OFLAG1(ROBOT)=OFLAG1(ROBOT).OR.NDSCBT	! don't describe robot.
	OFLAG1(ROBOT)=OR(OFLAG1(ROBOT),NDSCBT)	! don't describe robot.
	CFLAG(CEVSPH)=.TRUE.
	CTICK(CEVSPH)=10			! get out in 10 or else.
	RETURN
C
47200	CALL NEWSTA(SPHER,0,0,0,0)		! you're dead.
C	RFLAG(CAGER)=RFLAG(CAGER).OR.RMUNG	! mung cage room.
	RFLAG(CAGER)=OR(RFLAG(CAGER),RMUNG)	! mung cage room.
	RDESC1(CAGER)=147
	CALL JIGSUP(148)			! mung player.
	RETURN
C
47500	CALL NEWSTA(SPHER,0,0,0,0)		! robot tried,
	CALL NEWSTA(ROBOT,264,0,0,0)		! kill him.
	CALL NEWSTA(CAGE,0,HERE,0,0)		! insert mangled cage.
	GO TO 50				! go see if now dark.
C
47600	IF(PRSA.NE.LOOKIW) GO TO 10		! look in?
	OAPPLI=NOBJS(OACTIO(PALAN),ARG)		! do palantir function.
	RETURN
C
C O118--	Geometrical buttons
C
48000	IF(PRSA.NE.PUSHW) GO TO 10		! push?
	I=PRSO-SQBUT+1				! get button index.
	IF((I.LE.0).OR.(I.GE.4)) GO TO 10	! a button?
	IF(WINNER.NE.PLAYER) GO TO (48100,48200,48300),I
	CALL JIGSUP(265)			! you pushed, you die.
	RETURN
C
48100	I=267					! square, speed up.
	IF(CAROZF) I=266
	CAROZF=.TRUE.
	CALL RSPEAK(I)
	RETURN
C
48200	I=266					! round, slow down.
	IF(CAROZF) I=268
	CAROZF=.FALSE.
	CALL RSPEAK(I)
	RETURN
C
48300	CAROFF=.NOT.CAROFF			! triangle, flip carousel.
	IF(OROOM(IRBOX).NE.CAROU) GO TO 48400	! iron box in carousel?
	CALL RSPEAK(269)			! yes, thump.
C	OFLAG1(IRBOX)=OFLAG1(IRBOX).XOR.VISIBT	! complement visibility.
	OFLAG1(IRBOX)=XOR(OFLAG1(IRBOX),VISIBT)	! complement visibility.
C	IF(CAROFF) RFLAG(CAROU)=RFLAG(CAROU).AND. .NOT.RSEEN
	IF(CAROFF) RFLAG(CAROU)=AND(RFLAG(CAROU), COMPL(RSEEN))
	RETURN
C
48400	CALL RSPEAK(232)			! click.
	RETURN
C
C O119--	Flask function
C
49000	IF(PRSA.EQ.OPENW) GO TO 49100		! open?
	IF((PRSA.NE.MUNGW).AND.(PRSA.NE.THROWW)) GO TO 10
	CALL NEWSTA(FLASK,270,0,0,0)		! kill flask.
C 49100	RFLAG(HERE)=RFLAG(HERE).OR.RMUNG	! mung room.
49100	RFLAG(HERE)=OR (RFLAG(HERE),RMUNG)	! mung room.
	RDESC1(HERE)=271
	CALL JIGSUP(272)			! poisoned.
	RETURN
C
C O120--	Bucket function
C
50000	IF(ARG.EQ.1) GO TO 10			! read in?
	IF(ARG.EQ.2) GO TO 50400		! read out?
	IF(PRSA.NE.BURNW) GO TO 50100		! burn?
	CALL RSPEAK(928)			! can't.
	RETURN
C
50100	IF(PRSA.NE.KICKW) GO TO 10		! kick?
	CALL JIGSUP(1067)			! dead.
	RETURN
C
50400	IF((OCAN(WATER).NE.BUCKE).OR.BUCKTF) GO TO 50500
	BUCKTF=.TRUE.				! bucket at top.
	CFLAG(CEVBUC)=.TRUE.
	CTICK(CEVBUC)=100			! start countdown.
	CALL NEWSTA(BUCKE,290,TWELL,0,0)	! reposition bucket.
	GO TO 50900				! finish up.
C
50500	IF((OCAN(WATER).EQ.BUCKE).OR..NOT.BUCKTF) GO TO 10
	BUCKTF=.FALSE.
	CALL NEWSTA(BUCKE,291,BWELL,0,0)	! bucket at bottom.
50900	IF(AV.NE.BUCKE) RETURN			! not in bucket?
	F=MOVETO(OROOM(BUCKE),WINNER)		! move adventurer.
	F=RMDESC(0)				! describe room.
	RETURN
C page
C OAPPLI, PAGE 9
C
C O121--	Eatme cake
C
51000	IF((PRSA.NE.EATW).OR.(PRSO.NE.ECAKE).OR.
	&	(HERE.NE.ALICE)) GO TO 10	! eat cake in aliceroom?
	CALL NEWSTA(ECAKE,273,0,0,0)		! vanish cake.
C	OFLAG1(ROBOT)=OFLAG1(ROBOT).AND. .NOT.VISIBT	! vanish robot.
	OFLAG1(ROBOT)=AND(OFLAG1(ROBOT), COMPL(VISIBT))	! vanish robot.
	DO 51100 I=1,OLNT			! make objects big.
	  IF((OROOM(I).NE.ALICE).OR.(OSIZE(I).EQ.10000))
	&	GO TO 51100
	  OSIZE(I)=OSIZE(I)*64
	  OROOM(I)=ALISM
51100	CONTINUE
	OAPPLI=MOVETO(ALISM,WINNER)		! move to alice small.
	RETURN
C
C O122--	Icings
C
52000	IF(PRSA.NE.READW) GO TO 52200		! read?
	I=274					! cant read.
	IF(PRSI.NE.0) I=275			! through something?
	IF(PRSI.EQ.BOTTL) I=276			! through bottle?
	IF(PRSI.EQ.FLASK) I=277+(PRSO-ORICE)	! through flask?
	CALL RSPEAK(I)				! read flask.
	RETURN
C
52200	IF((PRSA.NE.THROWW).OR.(PRSO.NE.RDICE).OR.(PRSI.NE.POOL))
	&	GO TO 52300			! throw rdice at pool?
	CALL NEWSTA(POOL,280,0,0,0)		! vanish pool.
C	OFLAG1(SAFFR)=OFLAG1(SAFFR).OR.VISIBT	! materialize spices.
	OFLAG1(SAFFR)=OR(OFLAG1(SAFFR),VISIBT)	! materialize spices.
	RETURN
C
52300	IF((HERE.NE.ALICE).AND.(HERE.NE.ALISM).AND.(HERE.NE.ALITR))
	&	GO TO 10			! in wonderland?
	IF(((PRSA.NE.EATW).AND.(PRSA.NE.THROWW)).OR.
	&	(PRSO.NE.ORICE)) GO TO 52400	! throw orange ice?
	CALL NEWSTA(ORICE,0,0,0,0)		! vanish orange ice.
C	RFLAG(HERE)=RFLAG(HERE).OR.RMUNG	! vanish room.
	RFLAG(HERE)=OR(RFLAG(HERE),RMUNG)	! vanish room.
	RDESC1(HERE)=281
	CALL JIGSUP(282)			! vanish adventurer.
	RETURN
C
52400	IF((PRSA.NE.EATW).OR.(PRSO.NE.BLICE))
	&	GO TO 10			! eat blue ice?
	CALL NEWSTA(BLICE,283,0,0,0)		! vanish blue ice.
	IF(HERE.NE.ALISM) GO TO 52500		! in reduced room?
C	OFLAG1(ROBOT)=OFLAG1(ROBOT).OR.VISIBT	! materialize robot.
	OFLAG1(ROBOT)=OR(OFLAG1(ROBOT),VISIBT)	! materialize robot.
	DO 52450 I=1,OLNT			! enlarge world.
	  IF((OROOM(I).NE.HERE).OR.(OSIZE(I).EQ.10000))
	&	GO TO 52450
	  OROOM(I)=ALICE
	  OSIZE(I)=OSIZE(I)/64
52450	CONTINUE
	OAPPLI=MOVETO(ALICE,WINNER)		! return
	RETURN
C
52500	CALL JIGSUP(284)			! enlarged in wrong room.
	RETURN
C
C O123--	Brick
C
54000	IF(PRSA.NE.BURNW) GO TO 10		! burn?
	CALL NEWSTA(BRICK,0,0,0,0)		! vanish brick.
	CALL JIGSUP(150)			! boom!
	RETURN
C
C O124--	Myself
C
C 55000	IF((PRSA.NE.GIVEW).OR.
C	& ((OFLAG2(PRSO).AND.NOCHBT).NE.0)) GO TO 55100	! give?
55000	IF((PRSA.NE.GIVEW).OR.
	& ((AND(OFLAG2(PRSO),NOCHBT)).NE.0)) GO TO 55100	! give?
	IF(PRSO.NE.WATER) GO TO 55050		! water?
	CALL NEWSTA(WATER,615,0,0,0)		! slips through fingers.
	RETURN
C
55050	CALL NEWSTA(PRSO,2,0,0,PLAYER)		! done.
	RETURN
C
55100	IF(PRSA.NE.TAKEW) GO TO 55200		! take?
	CALL RSPEAK(286)			! joke.
	RETURN
C
55200	IF(((PRSA.NE.KILLW).AND.(PRSA.NE.MUNGW))
	&	.OR.(PRSO.NE.OPLAY)) GO TO 10
	WINNER=PLAYER				! can't kill someone else.
	CALL JIGSUP(287)			! kill, no joke.
	RETURN
C page
C OAPPLI, PAGE 10
C
C O125--	Panels inside mirror
C
56000	IF(PRSA.NE.PUSHW) GO TO 10		! push?
	IF(POLEUF.NE.0) GO TO 56100		! short pole up?
	I=731					! no, wont budge.
	IF(MOD(MDIR,180).EQ.0) I=732		! diff msg if n-s.
	CALL RSPEAK(I)				! tell wont move.
	RETURN
C
56100	IF(MLOC.NE.MRG) GO TO 56200		! in gdn room?
	CALL RSPEAK(733)			! you lose.
	CALL JIGSUP(685)
	RETURN
C
56200	I=831					! rotate l or r.
	IF((PRSO.EQ.RDWAL).OR.(PRSO.EQ.YLWAL)) I=830
	CALL RSPEAK(I)				! tell direction.
	MDIR=MOD(MDIR+45+(270*(I-830)),360)	! calculate new dir.
	CALL RSPSUB(734,695+(MDIR/45))		! tell new dir.
	IF(WDOPNF) CALL RSPEAK(730)		! if panel open, close.
	WDOPNF=.FALSE.
	RETURN					! done.
C
C O126--	Ends inside mirror
C
57000	IF(PRSA.NE.PUSHW) GO TO 10		! push?
	IF(MOD(MDIR,180).EQ.0) GO TO 57100	! mirror n-s?
	CALL RSPEAK(735)			! no, wont budge.
	RETURN
C
57100	IF(PRSO.NE.PINDR) GO TO 57300		! push pine wall?
	IF(((MLOC.EQ.MRC).AND.(MDIR.EQ.180)).OR.
	&  ((MLOC.EQ.MRD).AND.(MDIR.EQ.0)).OR.
	&   (MLOC.EQ.MRG)) GO TO 57200		! in view of gdn?
	CALL RSPEAK(736)			! no, opens.
	WDOPNF=.TRUE.				! indicate open.
	CFLAG(CEVPIN)=.TRUE.			! time opening.
	CTICK(CEVPIN)=5
	RETURN
C
57200	CALL RSPEAK(737)			! gdn sees you, die.
	CALL JIGSUP(685)
	RETURN
C
57300	NLOC=MLOC-1				! new loc if south.
	IF(MDIR.EQ.0) NLOC=MLOC+1		! new loc if north.
	IF((NLOC.GE.MRA).AND.(NLOC.LE.MRD)) GO TO 57400
	CALL RSPEAK(738)			! have reached end.
	RETURN
C
57400	I=699					! assume south.
	IF(MDIR.EQ.0) I=695			! north.
	J=739					! assume smooth.
	IF(POLEUF.NE.0) J=740			! pole up, wobbles.
	CALL RSPSUB(J,I)			! describe.
	MLOC=NLOC
	IF(MLOC.NE.MRG) RETURN			! now in gdn room?
C
	IF(POLEUF.NE.0) GO TO 57500		! pole up, gdn sees.
	IF(MROPNF.OR.WDOPNF) GO TO 57600	! door open, gdn sees.
	IF(MR1F.AND.MR2F) RETURN		! mirrors intact, ok.
	CALL RSPEAK(742)			! mirrors broken, die.
	CALL JIGSUP(743)
	RETURN
C
57500	CALL RSPEAK(741)			! pole up, die.
	CALL JIGSUP(743)
	RETURN
C
57600	CALL RSPEAK(744)			! door open, die.
	CALL JIGSUP(743)
	RETURN
C page
C OAPPLI, PAGE 11
C
C O127--	Global guardians
C
58000	IF((PRSA.NE.ATTACW).AND.(PRSA.NE.KILLW).AND.
	&  (PRSA.NE.MUNGW)) GO TO 58100		! aggressive?
	CALL JIGSUP(745)			! lose.
	RETURN
C
58100	IF(PRSA.NE.HELLOW) GO TO 10		! hello?
	CALL RSPEAK(746)			! no reply.
	RETURN
C
C O128--	Global master
C
59000	IF(((PRSA.NE.ATTACW).AND.(PRSA.NE.KILLW).AND.(PRSA.NE.MUNGW))
	&	.OR.(PRSO.NE.MASTER).OR.(PRSI.EQ.MASTER))
	&	GO TO 59100			! kill master?
	WINNER=PLAYER				! rebounds on player.
	CALL JIGSUP(747)			! bad idea.
	RETURN
C
59100	IF(PRSA.NE.TAKEW) GO TO 10		! take?
	CALL RSPEAK(748)			! joke.
	RETURN
C
C O129--	Numeral five
C
60000	IF(PRSA.NE.TAKEW) GO TO 10		! take five?
	CALL RSPEAK(419)			! time passes.
	DO 60100 I=1,3				! wait a while.
	  IF(CLOCKD(X)) RETURN
60100	CONTINUE
	RETURN
C
C O130--	Crypt function
C
61000	IF(.NOT.ENDGMF) GO TO 45000		! if not eg, die.
	IF(PRSA.NE.OPENW) GO TO 61100		! open?
	I=793
	IF(QOPEN(TOMB)) I=794
	CALL RSPEAK(I)
C	OFLAG2(TOMB)=OFLAG2(TOMB).OR.OPENBT	! now tomb with view.
	OFLAG2(TOMB)=OR(OFLAG2(TOMB),OPENBT)	! now tomb with view.
	RETURN
C
61100	IF(PRSA.NE.CLOSEW) GO TO 45000		! close?
	I=795
	IF(QOPEN(TOMB)) I=796
	CALL RSPEAK(I)
C	OFLAG2(TOMB)=OFLAG2(TOMB).AND..NOT.OPENBT
	OFLAG2(TOMB)=AND(OFLAG2(TOMB),COMPL(OPENBT))
	IF(HERE.NE.CRYPT) RETURN
	CFLAG(CEVSTE)=.TRUE.
	CTICK(CEVSTE)=3				! if in crypt, start eg.
	RETURN
C page
C OAPPLI, PAGE 12
C
C O131--	Global ladder
C
62000	IF((CPVEC(CPHERE+1).EQ.-2).OR.(CPVEC(CPHERE-1).EQ.-3))
	&	GO TO 62100			! ladder here?
	CALL RSPEAK(865)			! no, lose.
	RETURN
C
62100	IF((PRSA.EQ.CLMBW).OR.(PRSA.EQ.CLMBUW)) GO TO 62200
	CALL RSPEAK(866)			! climb it?
	RETURN
C
62200	IF((CPHERE.EQ.10).AND.(CPVEC(CPHERE+1).EQ.-2))
	&	GO TO 62300			! at exit?
	CALL RSPEAK(867)			! no, hit your head.
	RETURN
C
62300	F=MOVETO(CPANT,WINNER)			! to anteroom.
	F=RMDESC(3)				! describe.
	RETURN
C
	END
C page
C SOBJS-	Simple objects processor
C
C Declarations
C
	LOGICAL FUNCTION SOBJS(RI,ARG)
	IMPLICIT INTEGER (A-Z)
	%include dparam
	LOGICAL MOVETO,OPNCLS,LIT,WASLIT
	LOGICAL F,QOPEN
C
C Functions and data
C
C	QOPEN(R)=(OFLAG2(R).AND.OPENBT).NE.0
	QOPEN(R)=(AND(OFLAG2(R),OPENBT)).NE.0
C
	ODO2=0
	ODI2=0
	IF((PRSO.NE.0).AND.(PRSO.LE.OMAX)) ODO2=ODESC2(PRSO)
	IF(PRSI.NE.0) ODI2=ODESC2(PRSI)
	AV=AVEHIC(WINNER)
	SOBJS=.TRUE.
	WASLIT=LIT(HERE)
C
	GO TO (1000,3000,4000,6000,7000,8000,9000,
	& 13000,14000,16000,17000,
	& 21000,23000,24000,27000,28000,29000,30000,
	& 31000,33000,34000,36000,37000,38000,
	& 41000,42000,43000,44000,46000,
	& 53000,56000)
	&	RI
	CALL BUG(6,RI)
C
C Return here to declare false result.
C
10	SOBJS=.FALSE.
	RETURN
C
C Return here to test for light source change.
C
50	IF(WASLIT.AND..NOT.LIT(HERE)) CALL RSPEAK(406)
	RETURN
C page
C SOBJS, PAGE 3
C
C O1--	Gunk
C
1000	IF(OCAN(GUNK).EQ.0) GO TO 10		! not inside? f
	CALL NEWSTA(GUNK,122,0,0,0)		! falls apart.
	RETURN
C
C O2--	Trophy case
C
3000	IF(PRSA.NE.TAKEW) GO TO 10		! take?
	CALL RSPEAK(128)			! cant.
	RETURN
C
C O3--	Bottle
C
4000	IF((PRSA.NE.THROWW).OR.(PRSO.NE.BOTTL)) GO TO 4100	! throw?
	CALL NEWSTA(BOTTL,129,0,0,0)		! breaks.
	RETURN
C
4100	IF(PRSA.NE.MUNGW) GO TO 10		! mung?
	CALL NEWSTA(BOTTL,131,0,0,0)		! breaks.
	RETURN
C page
C SOBJS, PAGE 4
C
C O4--	Rope
C
6000	IF((HERE.EQ.DOME).OR.(HERE.EQ.SLIDE).OR.(PRSI.EQ.0).OR.
	&  (PRSI.EQ.TIMBE).OR.(PRSI.EQ.COFFI)) GO TO 6100
	IF(PRSA.EQ.TIEW) CALL RSPEAK(135)	! tie, cant do it.
6050	DOMEF=.FALSE.				! not tied in dome.
	TTIE=0					! not tied to timber.
C	OFLAG1(TIMBE)=OFLAG1(TIMBE).AND..NOT.NDSCBT
	OFLAG1(TIMBE)=AND(OFLAG1(TIMBE),COMPL(NDSCBT))
C	OFLAG1(COFFI)=OFLAG1(COFFI).AND..NOT.NDSCBT
	OFLAG1(COFFI)=AND(OFLAG1(COFFI),COMPL(NDSCBT))
	ODESC1(TIMBE)=1032			! restore timber, coffin
	ODESC1(COFFI)=1033
C	OFLAG1(ROPE)=OFLAG1(ROPE).AND..NOT.NDSCBT
	OFLAG1(ROPE)=AND(OFLAG1(ROPE),COMPL(NDSCBT))
C	OFLAG2(ROPE)=OFLAG2(ROPE).AND..NOT.CLMBBT
	OFLAG2(ROPE)=AND(OFLAG2(ROPE),COMPL(CLMBBT))
	RETURN					! rope not climbable
C
6100	IF((PRSA.NE.CLMBDW).OR.(HERE.NE.CPANT)) GO TO 6200
	IF(TTIE.EQ.0) GO TO 6150		! climb down, tied?
	IF(OROOM(TTIE).NE.HERE) GO TO 6150	! tied here?
	CALL RSPSUB(1028,ODESC2(TTIE))		! yes, tumbles after you.
	CALL NEWSTA(ROPE,0,CPUZZ,0,0)		! now in puzzle room.
	CALL NEWSTA(TTIE,0,CPUZZ,0,0)
	GO TO 10				! not handled here.
C
6150	CALL RSPEAK(1029)			! not tied.
	GO TO 10				! not handled here.
C
6200	IF(PRSA.NE.TIEW) GO TO 6400		! tie rope?
	IF(PRSI.NE.RAILI) GO TO 6300		! to railing?
	IF(DOMEF.OR.(TTIE.NE.0)) GO TO 6250	! already tied?
	DOMEF=.TRUE.				! no, now tied.
	CALL NEWSTA(ROPE,137,DOME,0,0)		! put in dome room.
C 6225	OFLAG1(ROPE)=OFLAG1(ROPE).OR.NDSCBT
6225	OFLAG1(ROPE)=OR(OFLAG1(ROPE),NDSCBT)
C	OFLAG2(ROPE)=OFLAG2(ROPE).OR.CLMBBT	! now climbable
	OFLAG2(ROPE)=OR(OFLAG2(ROPE),CLMBBT)	! now climbable
	RETURN
C
6250	CALL RSPEAK(136)			! already tied.
	RETURN
C
6300	IF((PRSI.NE.TIMBE).AND.(PRSI.NE.COFFI)) GO TO 10
	IF(DOMEF.OR.(TTIE.NE.0)) GO TO 6250	! already done?
	IF(OROOM(PRSI).NE.0) GO TO 6350		! target on ground?
	CALL RSPEAK(1025)			! too clumsy.
	RETURN
C
6350	CALL RSPSUB(961,ODI2)			! now tied to object.
	TTIE=PRSI
	IF(PRSI.EQ.TIMBE) ODESC1(TIMBE)=1030	! change description
	IF(PRSI.EQ.COFFI) ODESC1(COFFI)=1031	! of target.
	IF(HERE.EQ.CPANT) CALL RSPEAK(1056)	! room-specific words.
	IF(HERE.EQ.SLIDE) CALL RSPEAK(339)
C	IF(HERE.EQ.SLIDE) OFLAG1(PRSI)=OFLAG1(PRSI).OR.NDSCBT
	IF(HERE.EQ.SLIDE) OFLAG1(PRSI)=OR(OFLAG1(PRSI),NDSCBT)
	CALL NEWSTA(ROPE,0,HERE,0,0)		! put rope in room.
	GO TO 6225				! rope now climbable.
C
6400	IF(PRSA.NE.UNTIEW) GO TO 6600		! untie rope?
	IF(.NOT.DOMEF.AND.(TTIE.EQ.0)) GO TO 6500 ! tied to obj or railing?
	CALL RSPEAK(139)			! report and then
	GO TO 6050				! clean up all status.
C
6500	CALL RSPEAK(134)			! not tied to anything.
	RETURN
C
6600	IF(DOMEF.OR.(PRSA.NE.DROPW).OR.
	&	(HERE.NE.DOME)) GO TO 6700	! drop & untied from dome?
	CALL NEWSTA(ROPE,140,MTORC,0,0)		! yes, drop.
	RETURN
C
6700	IF((PRSA.NE.TAKEW).OR..NOT.DOMEF) GO TO 6800
	CALL RSPEAK(141)			! take & tied.
	RETURN
C
6800	IF((PRSA.NE.TAKEW).OR.(TTIE.EQ.0)) GO TO 10
	CALL RSPSUB(926,ODESC2(TTIE))		! take & tied.
	RETURN
C
C O5--	Sword
C
7000	IF((PRSA.EQ.TAKEW).AND.(WINNER.EQ.PLAYER))
	&	SWDACT=.TRUE.			! turn on demon.
	GO TO 10
C
C O6--	Lantern
C
8000	IF((PRSA.NE.THROWW).OR.(PRSO.NE.LAMP)) GO TO 8100	! throw?
	CALL NEWSTA(LAMP,0,0,0,0)		! kill lamp,
	CALL NEWSTA(BLAMP,142,HERE,0,0)		! replace with broken.
	IF(HERE.EQ.MTREE) CALL NEWSTA(BLAMP,0,FORE3,0,0)
	IF(LASTIT.EQ.LAMP) LASTIT=BLAMP		! fix last it reference.
	CFLAG(CEVLNT)=.FALSE.			! turn off timer.
	GO TO 50				! go see if now dark.
C
8100	IF(PRSA.EQ.TRNONW) CFLAG(CEVLNT)=.TRUE.
	IF(PRSA.EQ.TRNOFW) CFLAG(CEVLNT)=.FALSE.
	GO TO 10
C
C O7--	Rug
C
9000	IF(PRSA.NE.RAISEW) GO TO 9100		! raise?
	CALL RSPEAK(143)			! cant
	RETURN
C
9100	IF(PRSA.NE.TAKEW) GO TO 9200		! take?
	CALL RSPEAK(144)			! cant
	RETURN
C
9200	IF(PRSA.NE.MOVEW) GO TO 9300		! move?
	CALL RSPEAK(145+ORRUG)
	ORRUG=1
C	OFLAG1(DOOR)=OFLAG1(DOOR).OR.VISIBT	! reveal door.
	OFLAG1(DOOR)=OR(OFLAG1(DOOR),VISIBT)	! reveal door.
	RETURN
C
9300	IF((PRSA.NE.LOOKUW).OR.(ORRUG.NE.0).OR.
	&	QOPEN(DOOR)) GO TO 10		! look under rug?
	CALL RSPEAK(345)
	RETURN
C page
C SOBJS, PAGE 5
C
C O8--	Skeleton
C
13000	I=ROBRM(HERE,100,LLD2,0,0)+ROBADV(WINNER,LLD2,0,0)
	CALL RSPEAK(162)			! curses.
	RETURN
C
C O9--	Mirror
C
14000	IF(MIRRMF.OR.(PRSA.NE.RUBW)) GO TO 14500
C	MROOM=HERE.XOR.1			! calculate new rm.
	MROOM=XOR(HERE,1)			! calculate new rm.
	DO 14100 I=1,OLNT			! interchange objs.
	  IF(OROOM(I).EQ.HERE) OROOM(I)=-1
	  IF(OROOM(I).EQ.MROOM) OROOM(I)=HERE
	  IF(OROOM(I).EQ.-1) OROOM(I)=MROOM
14100	CONTINUE
	F=MOVETO(MROOM,WINNER)
	CALL RSPEAK(163)			! shake world.
	RETURN
C
14500	IF((PRSA.NE.LOOKW).AND.(PRSA.NE.LOOKIW).AND.
	&	(PRSA.NE.EXAMIW)) GO TO 14600
	I=164					! mirror ok.
	IF(MIRRMF) I=165			! mirror dead.
	CALL RSPEAK(I)
	RETURN
C
14600	IF(PRSA.NE.TAKEW) GO TO 14700		! take?
	CALL RSPEAK(166)			! joke.
	RETURN
C
14700	IF((PRSA.NE.MUNGW).AND.(PRSA.NE.THROWW)) GO TO 10
	I=167					! mirror breaks.
	IF(MIRRMF) I=168			! mirror already broken.
	MIRRMF=.TRUE.
	BADLKF=.TRUE.
	CALL RSPEAK(I)
	RETURN
C page
C SOBJS, PAGE 6
C
C O10--	Dumbwaiter
C
16000	IF(PRSA.NE.RAISEW) GO TO 16100		! raise?
	IF(CAGETF) GO TO 16400			! already at top?
	CALL NEWSTA(TBASK,175,TSHAF,0,0)	! no, raise basket.
	CALL NEWSTA(FBASK,0,BSHAF,0,0)
	IF(HERE.EQ.TSHAF) LASTIT=TBASK		! fix last it reference.
	IF(HERE.EQ.BSHAF) LASTIT=FBASK
	CAGETF=.TRUE.				! at top.
	RETURN
C
16100	IF(PRSA.NE.LOWERW) GO TO 16200		! lower?
	IF(.NOT.CAGETF) GO TO 16400		! already at bottom?
	CALL NEWSTA(TBASK,176,BSHAF,0,0)	! no, lower basket.
	CALL NEWSTA(FBASK,0,TSHAF,0,0)
	IF(HERE.EQ.TSHAF) LASTIT=FBASK		! fix last it reference.
	IF(HERE.EQ.BSHAF) LASTIT=TBASK
	CAGETF=.FALSE.
	GO TO 50				! go see if now dark.
C
16200	IF((PRSO.NE.FBASK).AND.(PRSI.NE.FBASK)) GO TO 16300
	CALL RSPEAK(130)			! wrong basket.
	RETURN
C
16300	IF(PRSA.NE.TAKEW) GO TO 10		! take?
	CALL RSPEAK(177)			! joke.
	RETURN
C
16400	CALL RSPEAK(125+RND(3))			! dummy.
	RETURN
C
C O11--	Ghost
C
17000	IF(PRSO.NE.GHOST) GO TO 17100		! direct object?
	CALL RSPEAK(178)			! joke.
	RETURN
C
17100	CALL RSPEAK(179)			! joke.
	GO TO 10				! don't handle.
C page
C SOBJS, PAGE 7
C
C O12--	Tube
C
21000	IF((PRSA.NE.PUTW).OR.(PRSI.NE.TUBE)) GO TO 21100
	CALL RSPEAK(186)			! cant put back in.
	RETURN
C
21100	IF(PRSA.NE.SQUEEW) GO TO 10		! squeeze?
	IF(QOPEN(PRSO)) GO TO 21200		! tube open?
	CALL RSPEAK(909)			! no, can't do it.
	RETURN
C
21200	IF(OCAN(PUTTY).EQ.PRSO) GO TO 21300	! putty inside?
	CALL RSPEAK(910)			! no, doesn't work.
	RETURN
C
21300	CALL NEWSTA(PUTTY,911,0,0,WINNER)	! putty now in hand.
	RETURN
C
C O13--	Chalice
C
C 23000	IF((PRSA.NE.TAKEW).OR.(OCAN(PRSO).NE.0).OR.
C	&	(OROOM(PRSO).NE.TREAS).OR.(OROOM(THIEF).NE.TREAS).OR.
C	&	((OFLAG2(THIEF).AND.FITEBT).EQ.0).OR.
C	&	.NOT. THFACT) GO TO 10
23000	IF((PRSA.NE.TAKEW).OR.(OCAN(PRSO).NE.0).OR.
	&	(OROOM(PRSO).NE.TREAS).OR.(OROOM(THIEF).NE.TREAS).OR.
	&	((AND(OFLAG2(THIEF),FITEBT)).EQ.0).OR.
	&	.NOT. THFACT) GO TO 10
	CALL RSPEAK(204)			! cant take.
	RETURN
C
C O14--	Painting
C
24000	IF(PRSA.NE.MUNGW) GO TO 10		! mung?
	CALL RSPEAK(205)			! destroy painting.
	OFVAL(PRSO)=0
	OTVAL(PRSO)=0
	ODESC1(PRSO)=207
	ODESC2(PRSO)=206
	RETURN
C page
C SOBJS, PAGE 8
C
C O15--	Bolt
C
27000	IF(PRSA.NE.TURNW) GO TO 27600		! turn bolt?
	IF(PRSI.NE.WRENC) GO TO 27500		! with wrench?
	IF(GATEF) GO TO 27100			! proper button pushed?
	CALL RSPEAK(210)			! no, lose.
	RETURN
C
27100	IF(LWTIDF) GO TO 27200			! low tide now?
	LWTIDF=.TRUE.				! no, empty dam.
	CALL RSPEAK(211)
C	OFLAG2(COFFI)=OFLAG2(COFFI).AND. .NOT.SCRDBT
	OFLAG2(COFFI)=AND(OFLAG2(COFFI), COMPL(SCRDBT))
C	OFLAG1(TRUNK)=OFLAG1(TRUNK).OR.VISIBT	! materialize trunk.
	OFLAG1(TRUNK)=OR(OFLAG1(TRUNK),VISIBT)	! materialize trunk.
C	RFLAG(RESER)=(RFLAG(RESER).OR.RLAND)
C	&	.AND..NOT.(RWATER+RSEEN)	! keep thief away.
	RFLAG(RESER)=AND(OR(RFLAG(RESER),RLAND),
	&	COMPL(RWATER+RSEEN))	! keep thief away.
	RETURN
C
27200	LWTIDF=.FALSE.				! yes, fill dam.
	CALL RSPEAK(212)
C	IF(OROOM(TRUNK).EQ.RESER) OFLAG1(TRUNK)=OFLAG1(TRUNK)
C	&	.AND. .NOT.VISIBT
	IF(OROOM(TRUNK).EQ.RESER) OFLAG1(TRUNK)=AND(OFLAG1(TRUNK),
	&	COMPL(VISIBT))
C	RFLAG(RESER)=(RFLAG(RESER).OR.RWATER) .AND..NOT.RLAND
	RFLAG(RESER)=AND(OR(RFLAG(RESER),RWATER),COMPL(RLAND))
	RETURN
C
27500	CALL RSPSUB(299,ODI2)			! not with that.
	RETURN
C
27600	IF(PRSA.NE.OILW) GO TO 10		! oil?
	CALL RSPEAK(906)			! trouble.
	RETURN
C
C O16--	Grating
C
28000	IF((PRSA.NE.OPENW).AND.(PRSA.NE.CLOSEW)) GO TO 10
	IF(GRUNLF) GO TO 28100			! unlocked?
	CALL RSPEAK(214)			! no, locked.
	RETURN
C
28100	I=215					! unlocked, view frm below.
	IF(HERE.EQ.CLEAR) I=216			! view from clearing
	SOBJS=OPNCLS(GRATE,I,885)		! open/close.
C	RFLAG(MGRAT)=RFLAG(MGRAT).AND. .NOT.RLIGHT	! set light/dark.
	RFLAG(MGRAT)=AND(RFLAG(MGRAT), COMPL(RLIGHT))	! set light/dark.
	IF(.NOT.QOPEN(GRATE)) GO TO 50		! if not open, done.
C	RFLAG(MGRAT)=RFLAG(MGRAT).OR.RLIGHT	! now lit.
	RFLAG(MGRAT)=OR(RFLAG(MGRAT),RLIGHT)	! now lit.
	RVCLR=1					! leaves shoved aside.
	CALL NEWSTA(GRATE,0,CLEAR,0,0)		! grating in clearing.
	RETURN
C
C O17--	Trap door
C
29000	IF(HERE.NE.LROOM) GO TO 29100		! from living room?
	SOBJS=OPNCLS(DOOR,218,219)		! open/close.
	RETURN
C
29100	IF(HERE.NE.CELLA) GO TO 10		! from cellar?
	IF((PRSA.NE.OPENW).OR.QOPEN(DOOR)) GO TO 29200
	CALL RSPEAK(220)			! cant open closed door.
	RETURN
C
29200	SOBJS=OPNCLS(DOOR,0,22)			! normal open/close.
	RETURN
C
C O18--	Durable door
C
30000	I=0					! assume no appl.
	IF(PRSA.EQ.OPENW) I=221			! open?
	IF(PRSA.EQ.BURNW) I=222			! burn?
	IF(PRSA.EQ.MUNGW) I=223+RND(3)		! mung?
	IF(I.EQ.0) GO TO 10
	CALL RSPEAK(I)
	RETURN
C
C O19--	Master switch
C
31000	IF(PRSA.NE.TURNW) GO TO 10		! turn?
	IF(PRSI.NE.SCREW) GO TO 31500		! with screwdriver?
	IF(QOPEN(MACHI)) GO TO 31600		! lid up?
	CALL RSPEAK(226)			! no, activate.
	IF(OCAN(COAL).NE.MACHI) GO TO 31400	! coal inside?
	CALL NEWSTA(COAL,0,0,0,0)		! kill coal,
	CALL NEWSTA(DIAMO,0,0,MACHI,0)		! replace with diamond.
	RETURN
C
31400	DO 31450 I=1,OLNT			! kill noncoal objects.
	  IF(OCAN(I).NE.MACHI) GO TO 31450	! inside machine?
	  CALL NEWSTA(I,0,0,0,0)		! kill object and contents.
	  CALL NEWSTA(GUNK,0,0,MACHI,0)		! reduce to gunk.
31450	CONTINUE
	RETURN
C
31500	CALL RSPSUB(300,ODI2)			! cant turn with that.
	RETURN
C
31600	CALL RSPEAK(227)			! lid is up.
	RETURN
C page
C SOBJS, PAGE 9
C
C O20--	Leak
C
33000	IF((PRSO.NE.LEAK).OR.(PRSA.NE.PLUGW).OR.(RVMNT.LE.0))
	&	GO TO 10			! plug active leak?
	IF(PRSI.NE.PUTTY) GO TO 33100		! with putty?
	RVMNT=-1				! disable leak.
	CTICK(CEVMNT)=0
	CALL RSPEAK(577)
	RETURN
C
33100	CALL RSPSUB(301,ODI2)			! cant with that.
	RETURN
C
C O21--	Drowning buttons
C
34000	IF(PRSA.NE.PUSHW) GO TO 10		! push?
	GO TO (34100,34200,34300,34400),(PRSO-RBUTT+1)
	GO TO 10				! not a button.
C
C 34100	RFLAG(HERE)=RFLAG(HERE).XOR.RLIGHT	! red, zap lights.
34100	RFLAG(HERE)=XOR(RFLAG(HERE),RLIGHT)	! red, zap lights.
	I=230
C	IF((RFLAG(HERE).AND.RLIGHT).NE.0) I=231
	IF((AND(RFLAG(HERE),RLIGHT)).NE.0) I=231
	CALL RSPEAK(I)
	GO TO 50				! go see if now dark.
C
34200	GATEF=.TRUE.				! yellow, release gate.
	CALL RSPEAK(232)
	RETURN
C
34300	GATEF=.FALSE.				! brown, interlock gate.
	CALL RSPEAK(232)
	RETURN
C
34400	IF(RVMNT.NE.0) GO TO 34500		! blue, leak already started?
	CALL RSPEAK(233)			! no, start leak.
	RVMNT=1
	CFLAG(CEVMNT)=.TRUE.
	CTICK(CEVMNT)=-1
C	RFLAG(HERE)=RFLAG(HERE).OR.RFILL	! water present.
	RFLAG(HERE)=OR(RFLAG(HERE),RFILL)	! water present.
C	OFLAG1(LEAK)=OFLAG1(LEAK).OR.VISIBT	! bring on the leak.
	OFLAG1(LEAK)=OR(OFLAG1(LEAK),VISIBT)	! bring on the leak.
	RETURN
C
34500	CALL RSPEAK(234)			! button jammed.
	RETURN
C
C O22--	Inflatable boat
C
36000	IF(PRSA.NE.INFLAW) GO TO 10		! inflate?
	IF(OROOM(IBOAT).NE.0) GO TO 36100	! in room?
	CALL RSPEAK(235)			! no, joke.
	RETURN
C
36100	IF(PRSI.NE.PUMP) GO TO 36200		! with pump?
	CALL NEWSTA(IBOAT,0,0,0,0)		! kill defl boat,
	CALL NEWSTA(RBOAT,236,HERE,0,0)		! repl with inf.
	IF(LASTIT.EQ.IBOAT) LASTIT=RBOAT	! fix last it reference.
	DEFLAF=.FALSE.
	RETURN
C
36200	I=237					! jokes.
	IF(PRSI.NE.LUNGS) I=303
	CALL RSPSUB(I,ODI2)
	RETURN
C
C O23--	Deflated boat
C
37000	IF(PRSA.NE.INFLAW) GO TO 37100		! inflate?
	CALL RSPEAK(238)			! joke.
	RETURN
C
37100	IF(PRSA.NE.PLUGW) GO TO 10		! plug?
	IF(PRSI.NE.PUTTY) GO TO 33100		! with putty?
	CALL NEWSTA(IBOAT,239,OROOM(DBOAT),OCAN(DBOAT),OADV(DBOAT))
	CALL NEWSTA(DBOAT,0,0,0,0)		! kill defl boat, repl.
	IF(LASTIT.EQ.DBOAT) LASTIT=IBOAT	! fix last it reference.
	RETURN
C page
C SOBJS, PAGE 10
C
C O24--	Rubber boat
C
38000	IF(ARG.NE.0) GO TO 10			! dismiss readin, out.
	IF((PRSA.NE.BOARDW).OR.(OADV(STICK).NE.WINNER)) GO TO 38100
	CALL NEWSTA(RBOAT,0,0,0,0)		! kill infl boat,
	CALL NEWSTA(DBOAT,240,HERE,0,0)		! repl with dead.
	IF(LASTIT.EQ.RBOAT) LASTIT=DBOAT	! fix last it reference.
	DEFLAF=.TRUE.
	GO TO 50				! go see if now dark.
C
38100	IF(PRSA.NE.INFLAW) GO TO 38200		! inflate?
	CALL RSPEAK(367)			! yes, joke.
	RETURN
C
38200	IF(PRSA.NE.DEFLAW) GO TO 10		! deflate?
	IF(AV.EQ.RBOAT) GO TO 38300		! in boat?
	IF(OROOM(RBOAT).EQ.0) GO TO 38400	! on ground?
	CALL NEWSTA(RBOAT,0,0,0,0)		! kill infl boat,
	CALL NEWSTA(IBOAT,241,HERE,0,0)		! repl with defl.
	IF(LASTIT.EQ.RBOAT) LASTIT=IBOAT	! fix last it reference.
	DEFLAF=.TRUE.
	GO TO 50				! go see if now dark.
C
38300	CALL RSPEAK(242)			! in boat.
	RETURN
C
38400	CALL RSPEAK(243)			! not on ground.
	RETURN
C
C O25--	Braided rope (also balloon receptacle, cloth bag)
C
41000	IF((PRSA.NE.TIEW).OR.(PRSO.NE.BROPE).OR.
	&	((PRSI.NE.HOOK1).AND.(PRSI.NE.HOOK2)))
	&	GO TO 41100			! tie to hook?
	BTIEF=PRSI				! record location.
	ODESC1(BTIEF)=1072			! change description.
	CFLAG(CEVBAL)=.FALSE.			! stall ascent.
	CALL RSPEAK(248)
	RETURN
C
41100	IF((PRSA.NE.UNTIEW).OR.(PRSO.NE.BROPE)) GO TO 41300
	IF(BTIEF.NE.0) GO TO 41200		! tied up?
	CALL RSPEAK(249)			! no, joke.
	RETURN
C
41200	CALL RSPEAK(250)
	ODESC1(BTIEF)=1073			! restore description.
	BTIEF=0					! untie.
	CFLAG(CEVBAL)=.TRUE.
	CTICK(CEVBAL)=3				! restart clock.
	RETURN
C
41300	IF((PRSA.NE.FINDW).AND.(PRSA.NE.EXAMIW)) GO TO 41400
	CALL RSPSUB(1063,ODO2)			! describe.
	RETURN
C
41400	IF(PRSA.NE.TAKEW) GO TO 10		! take?
	CALL RSPSUB(1064,ODO2)			! can't.
	IF(PRSO.EQ.BROPE) CALL RSPEAK(1065)	! rope can be tied.
	RETURN
C
C O26--	Safe
C
42000	I=0					! assume unprocessed.
	IF(PRSA.EQ.TAKEW) I=251			! take?
	IF((PRSA.EQ.OPENW).AND.SAFEF) I=253	! open after blast?
	IF((PRSA.EQ.OPENW).AND..NOT.SAFEF) I=254 ! open before blast?
	IF((PRSA.EQ.CLOSEW).AND.SAFEF) I=253	! close after?
	IF((PRSA.EQ.CLOSEW).AND..NOT.SAFEF) I=255
	IF(I.EQ.0) GO TO 10
	CALL RSPEAK(I)
	RETURN
C
C O27--	Fuse
C
43000	IF(PRSA.NE.BURNW) GO TO 10		! burn?
	CALL RSPEAK(256)
	CFLAG(CEVFUS)=.TRUE.
	CTICK(CEVFUS)=2				! start countdown.
	RETURN
C
C O28--	Gnome
C
44000	IF((PRSA.NE.GIVEW).AND.(PRSA.NE.THROWW)) GO TO 44500
	IF(OTVAL(PRSO).EQ.0) GO TO 44100	! treasure?
	CALL RSPSUB(257,ODO2)			! yes, get door.
	CALL NEWSTA(PRSO,0,0,0,0)
	CALL NEWSTA(GNOME,0,0,0,0)		! vanish gnome.
	GNODRF=.TRUE.
	GO TO 50				! go see if now dark.
C
44100	IF((PRSO.NE.BRICK).OR.(OCAN(FUSE).NE.BRICK).OR.
	&	(CTICK(CEVFUS).EQ.0)) GO TO 44200 ! a bomb?
	CALL NEWSTA(GNOME,927,0,0,0)		! gnome leaves.
	CALL NEWSTA(BRICK,0,HERE,0,0)		! brick on floor.
	CFLAG(CEVVLG)=.FALSE.			! turn off gnome clocks.
	CFLAG(CEVGNO)=.FALSE.
	RETURN
C
44200	CALL RSPSUB(258,ODO2)			! no, lose object.
	CALL NEWSTA(PRSO,0,0,0,0)
	GO TO 50				! go see if now dark.
C
44500	CALL RSPEAK(259)			! nervous gnome.
	IF(GNOMEF) RETURN
	CFLAG(CEVGNO)=.TRUE.
	CTICK(CEVGNO)=5				! schedule byebye.
	GNOMEF=.TRUE.
	RETURN
C
C O29--	Coke bottles
C
46000	IF((PRSA.NE.THROWW).AND.(PRSA.NE.MUNGW)) GO TO 10
	CALL NEWSTA(COKES,262,0,0,0)		! mung bottles.
	IF(PRSI.NE.COKES) RETURN		! with cokes?
	CALL RSPSUB(1066,ODO2)			! kill direct object, too.
	CALL NEWSTA(PRSO,0,0,0,0)
	RETURN
C page
C SOBJS, PAGE 11
C
C O30--	Robot
C
53000	IF(PRSA.NE.GIVEW) GO TO 53200		! give?
	IF(PRSO.NE.WATER) GO TO 53100		! water?
	CALL NEWSTA(WATER,1081,0,0,0)		! slips through fingers.
	RETURN
C
53100	CALL NEWSTA(PRSO,0,0,0,AROBOT)		! put on robot.
	CALL RSPSUB(302,ODO2)
	RETURN
C
53200	IF(((PRSA.NE.MUNGW).AND.(PRSA.NE.THROWW)).OR.
	&  ((PRSO.NE.ROBOT).AND.(PRSI.NE.ROBOT))) GO TO 10
	CALL NEWSTA(ROBOT,285,0,0,0)		! kill robot.
	GO TO 50				! go see if now dark.
C
C O31--	Grue
C
56000	IF(PRSA.NE.EXAMIW) GO TO 56100		! examine?
	CALL RSPEAK(288)
	RETURN
C
56100	IF(PRSA.NE.FINDW) GO TO 10		! find?
	CALL RSPEAK(289)
	RETURN
C
	END
